buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5'
    }
}

apply plugin: 'war'
apply plugin: 'cargo-base'

description "Admin web application"


sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/conf'
            srcDir 'src/java'
        }
    }
    test {
	java {
            srcDir 'src/test'
	}
	resources {
	    srcDir 'src/test'
	}
    }
}







dependencies {

    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'javax.servlet:jsp-api:2.0'
    providedCompile 'javax.portlet:portlet-api:2.0'

    compile project(':fedora-api')
    compile project(':common')
    compile project(':static-export')
    compile project(':indexer')
    compile project(':import')
    compile project(':import-mets')
    compile project(':security-core')
    compile project(':rest')

    compile 'javax.servlet:jstl:1.1.2'   
    compile 'taglibs:standard:1.1.2'   
    compile 'xalan:xalan:2.7.1'   
    compile 'net.sf:simplecaptcha:1.2.1'   
    compile 'xerces:xercesImpl:2.9.1'   
    compile 'xerces:xercesImpl:2.9.1'   
    compile 'com.foxtrottechnologies:javadjvu:0.8.09'
    compile 'com.foxtrottechnologies:djvuframe:0.8.09'

    compile "org.slf4j:slf4j-api:${slf4jversion}"
    compile "org.slf4j:log4j-over-slf4j:${slf4jversion}"
    compile "org.slf4j:slf4j-jdk14:${slf4jversion}"

    compile 'net.sf.ehcache:ehcache:2.6.0'
    compile 'org.apache.httpcomponents:httpclient:4.1'
    compile 'net.sf.json-lib:json-lib:2.4:jdk15'
}


task gitcall(type:Exec) {
   commandLine "git","rev-parse","HEAD"
   standardOutput = new ByteArrayOutputStream()
   doLast{
        byte[] bytes = standardOutput.toByteArray()
        ext.hash =new String(bytes).trim(); 
   }     
}


task infofile(dependsOn: gitcall) << {
    def parentFolder = new File("$buildDir/resources/main")
    parentFolder.mkdirs()
       
    def buildInfoFile = new File("$buildDir/resources/main/build.properties")
    Properties props = new Properties()
    props.setProperty('version', project.version.toString())
    props.setProperty('hash', gitcall.hash)

    props.store(buildInfoFile.newWriter(), null)
    println "Created build file ${buildInfoFile.absolutePath}"
    
}

processResources.dependsOn infofile



war {
    baseName='search'
    archiveName ='search.war'
    from 'web' 
}




/**
 * Deploy to localhost, vmkramerius
 */
class RemoteContainer {
    String name
    String hostname
    Integer port
    String username
    String password
    String tomcatId
}


def remoteContainers = [

                        new RemoteContainer(name: 'local', hostname: 'localhost',
                                            port: 8080, username: 'tomcat', password: 'tomcat', tomcatId:'tomcat6x'),
]

if (hasProperty('vmkrameriusAdm') && hasProperty('vmkrameriusPass') && hasProperty('vmkrameriusTomcatId')) {
        remoteContainers.add(new RemoteContainer(name: 'vmkram', hostname: 'vmkramerius',
            port: 8080, username: getProperty('vmkrameriusAdm'), password: getProperty('vmkrameriusPass'), tomcatId:getProperty('vmkrameriusTomcatId')));
}


remoteContainers.each { config ->
    task "deploy${config.name.capitalize()}"(type: org.gradle.api.plugins.cargo.tasks.remote.CargoDeployRemote) {
        description = "Deploys WAR to remote Tomcat '${config.name}'."
        containerId = config.tomcatId
        hostname = config.hostname
        port = config.port
        username = config.username
        password = config.password
    }

    task "undeploy${config.name.capitalize()}"(type: org.gradle.api.plugins.cargo.tasks.remote.CargoUndeployRemote) {
        description = "UnDeploys WAR to remote Tomcat '${config.name}'."
        containerId = config.tomcatId
        hostname = config.hostname
        port = config.port
        username = config.username
        password = config.password
    }
}


